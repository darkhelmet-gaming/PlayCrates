import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "xyz.jpenilla.run-paper" version "1.0.6"
    id "com.github.ben-manes.versions" version "0.42.0"
}

apply plugin: 'java'
apply plugin: 'checkstyle'

processResources {
    filter ReplaceTokens, tokens: [
        "apiversion": project.property("apiversion"),
        "version"   : project.property("version")
    ]
}

repositories {
    mavenLocal()
    mavenCentral()

    // Aikar
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }

    // Spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    // CodeMC (nbt-api)
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }

    // Sonatype (kyori)
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }

    // TeamTriumph
    maven { url = 'https://repo.triumphteam.dev/snapshots' }
}

group = project.property("group")
version = project.property("version")
targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.17.2'
    implementation 'com.google.inject:guice:5.1.0'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'net.kyori:adventure-api:4.11.0'
    implementation 'net.kyori:adventure-serializer-configurate4:4.11.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.1.1'
    implementation 'net.kyori:adventure-text-serializer-plain:4.11.0'
    implementation 'net.kyori:adventure-text-minimessage:4.11.0'
    implementation 'net.kyori.moonshine:moonshine-core:2.0.4'
    implementation 'net.kyori.moonshine:moonshine-standard:2.0.4'
    implementation 'org.spongepowered:configurate-hocon:4.1.2'
    implementation 'dev.triumphteam:triumph-cmd-bukkit:2.0.0-SNAPSHOT'
    implementation 'co.aikar:idb-core:1.0.0-SNAPSHOT'
    implementation 'co.aikar:taskchain-bukkit:3.7.2'
    implementation 'de.tr7zw:item-nbt-api-plugin:2.10.0'
    implementation project(':playcrates-api')
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

jar {
    actions = []
    dependsOn = []
    dependsOn('shadowJar')
}

shadowJar {
    relocate 'com.google.inject', 'network.darkhelmet.playcrates.inject'
    relocate 'co.aikar.idb', 'network.darkhelmet.playcrates.idb'
    relocate 'co.aikar.taskchain', 'network.darkhelmet.playcrates.taskchain'
    relocate 'com.zaxxer', 'network.darkhelmet.playcrates.zaxxer'
    relocate 'net.kyori', 'network.darkhelmet.playcrates.kyori'
    relocate 'org.spongepowered', 'network.darkhelmet.playcrates.spongepowered'
    relocate 'de.tr7zw', 'network.darkhelmet.playcrates.tr7zw'

    minimize()
}

runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion("1.19")
}